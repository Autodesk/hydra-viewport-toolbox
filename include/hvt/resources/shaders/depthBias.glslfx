-- glslfx version 0.1

// Copyright 2025 Autodesk, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

-- configuration
{
    "techniques": {
        "default": {
            "DepthBias::Fragment": {
                "source": [ "DepthBias.Fragment" ]
            }
        }
    }
}

-- glsl DepthBias.Fragment

// Computes the view-space Z from the depth (non-linear when perspective), using the clip info
// structure having the following members: {zNear * zFar, zNear - zFar, zFar}.
float computeZFromDepth(float depth)
{
    float z = uIsOrthographic > 0 ?
        uClipInfo.x / uClipInfo.z - uClipInfo.y * depth :
        uClipInfo.x / (uClipInfo.y * depth + uClipInfo.z);
    return z;
}

// Computes the depth buffer value from view-space Z.
float computeDepthFromZ(float z)
{
    float depth = uIsOrthographic > 0 ?
        (z - uClipInfo.x / uClipInfo.z) / (-uClipInfo.y) :
        (uClipInfo.x / z - uClipInfo.z) / uClipInfo.y;
    return depth;
}

void main(void)
{
    // Get the fragment location, i.e. the screen-space position in pixels.
    ivec2 location = ivec2(uvOut * uScreenSize);

    // Get the depth value.
    float inDepth = HgiTexelFetch_depthIn(location).x;
    // Get the color value.
    vec4 inColor = HgiTexelFetch_colorIn(location);

    float viewSpaceZ = computeZFromDepth(inDepth);
    float newViewSpaceZ = viewSpaceZ + uViewSpaceDepthOffset;
    float newDepth = computeDepthFromZ(newViewSpaceZ);
    newDepth = clamp(newDepth, 0.0, 1.0);

    // Push the new values.
    gl_FragDepth = newDepth;
    hd_FragColor = inColor;
}
